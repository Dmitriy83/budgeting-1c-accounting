#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

// Обработка проведения.
// 
// Параметры:
//  Отказ 			- Булево 					- Отказ
//  РежимПроведения - РежимПроведенияДокумента 	- Режим проведения
Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	ПроведениеСервер.ПодготовитьНаборыЗаписейКРегистрацииДвижений(ЭтотОбъект);
	ДанныеДляПроведения = ДанныеДляПроведенияДокумента();
	Движения.бджПланБюджетаДоходовИРасходов.Записывать = Истина;
	Для Каждого ПланСтрока Из ДанныеДляПроведения.План Цикл
		Движение = Движения.бджПланБюджетаДоходовИРасходов.Добавить();
		ЗаполнитьЗначенияСвойств(Движение, ПланСтрока);
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Данные для проведения документа.
// 
// Возвращаемое значение:
//  Структура - Данные для проведения документа:
// * План - ТаблицаЗначений
//
Функция ДанныеДляПроведенияДокумента()
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	План.СтатьяБюджетаДоходовИРасходов КАК СтатьяБюджетаДоходовИРасходов,
	|	План.Аналитика КАК Аналитика,
	|	СУММА(План.Значение) КАК Значение,
	|	План.Ссылка КАК Регистратор,
	|	План.Ссылка.Месяц КАК Период,
	|	План.Ссылка.Организация
	|ИЗ
	|	Документ.бджВводПланаБюджетаДоходовИРасходов.План КАК План
	|ГДЕ
	|	План.Ссылка = &Ссылка
	|СГРУППИРОВАТЬ ПО
	|	План.СтатьяБюджетаДоходовИРасходов,
	|	План.Аналитика,
	|	План.Ссылка,
	|	План.Ссылка.Месяц,
	|	План.Ссылка.Организация";
	Возврат Новый Структура("План", Запрос.Выполнить().Выгрузить());

КонецФункции

#КонецОбласти

#Иначе
ВызватьИсключение НСтр("ru = 'Недопустимый вызов объекта на клиенте.'");
#КонецЕсли